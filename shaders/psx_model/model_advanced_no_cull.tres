[gd_resource type="Shader" load_steps=0 format=3 uid="uid://nfawvsh5fc7x"]

[resource]
code = "// NOTE: Shader automatically c;onverted from Godot Engine 4.4.dev6's StandardMaterial3D.

shader_type spatial;
render_mode
blend_mix
, cull_disabled
,shadows_disabled
,alpha_to_coverage
,depth_prepass_alpha
,specular_disabled
;


uniform bool affine_mapping = false;
uniform float jitter: hint_range(0, 1) = 0.25;
uniform ivec2 resolution = ivec2(480, 270);

uniform vec4 albedo : source_color = vec4(1.);
uniform sampler2D texture_albedo : source_color, filter_nearest, repeat_enable;

uniform bool triplanar = false;

varying vec3 uv1_triplanar_pos;
uniform float uv1_blend_sharpness : hint_range(0.0, 150.0, 0.001) = 1.0f;
varying vec3 uv1_power_normal;

uniform vec3 uv1_scale = vec3(1.0);
uniform vec3 uv1_offset;


group_uniforms ProximityFade;
uniform bool proximity_fade = false;
uniform float proximity_fade_distance : hint_range(0.0, 4096.0, 0.01) = 1.0;


group_uniforms OverlayTexture;
uniform bool overlay_texture = false;
uniform sampler2D texture_albedo_2 : source_color, filter_nearest, repeat_enable;
uniform bool triplanar_2 = false;
varying vec3 uv2_triplanar_pos;
uniform float uv2_blend_sharpness : hint_range(0.0, 150.0, 0.001) = 1.0f;
varying vec3 uv2_power_normal;
uniform vec3 uv2_scale;
uniform vec3 uv2_offset;

uniform sampler2D depth_texture : hint_depth_texture, repeat_disable, filter_nearest;


vec4 snap_to_position(vec4 base_position)
{
	vec4 snapped_position = base_position;
	snapped_position.xyz = base_position.xyz / base_position.w;

	vec2 snap_resulotion = floor(vec2(resolution) * (1.0 - jitter));
	snapped_position.x = floor(snap_resulotion.x * snapped_position.x) / snap_resulotion.x;
	snapped_position.y = floor(snap_resulotion.y * snapped_position.y) / snap_resulotion.y;

	snapped_position.xyz *= base_position.w;
	return snapped_position;
}

void vertex() {
	vec3 normal = MODEL_NORMAL_MATRIX * NORMAL;

	TANGENT = vec3(0.0, 0.0, -1.0) * abs(normal.x);
	TANGENT += vec3(1.0, 0.0, 0.0) * abs(normal.y);
	TANGENT += vec3(1.0, 0.0, 0.0) * abs(normal.z);
	TANGENT = inverse(MODEL_NORMAL_MATRIX) * normalize(TANGENT);

	BINORMAL = vec3(0.0, 1.0, 0.0) * abs(normal.x);
	BINORMAL += vec3(0.0, 0.0, -1.0) * abs(normal.y);
	BINORMAL += vec3(0.0, 1.0, 0.0) * abs(normal.z);
	BINORMAL = inverse(MODEL_NORMAL_MATRIX) * normalize(BINORMAL);

	vec4 snapped_position = snap_to_position(PROJECTION_MATRIX * MODELVIEW_MATRIX * vec4(VERTEX, 1.0));
	if (affine_mapping)
	{
		POSITION = snapped_position;
		POSITION /= abs(POSITION.w);
	}
	else
	{
		POSITION = snapped_position;
	}

	if (triplanar) {
		// UV1 Triplanar: Enabled (with World Triplanar)
		uv1_power_normal = pow(abs(normal), vec3(uv1_blend_sharpness));
		uv1_triplanar_pos = (MODEL_MATRIX * vec4(VERTEX, 1.0)).xyz * uv1_scale + uv1_offset;
		uv1_power_normal /= dot(uv1_power_normal, vec3(1.0));
		uv1_triplanar_pos *= vec3(1.0, -1.0, 1.0);
	}

	if (triplanar_2 && overlay_texture) {
		// UV1 Triplanar: Enabled (with World Triplanar)
		uv2_power_normal = pow(abs(normal), vec3(uv2_blend_sharpness));
		uv2_triplanar_pos = (MODEL_MATRIX * vec4(VERTEX, 1.0)).xyz * uv2_scale + uv2_offset;
		uv2_power_normal /= dot(uv2_power_normal, vec3(1.0));
		uv2_triplanar_pos *= vec3(1.0, -1.0, 1.0);
	}
}

vec4 triplanar_texture(sampler2D p_sampler, vec3 p_weights, vec3 p_triplanar_pos) {
	vec4 samp = vec4(0.0);
	samp += texture(p_sampler, p_triplanar_pos.xy) * p_weights.z;
	samp += texture(p_sampler, p_triplanar_pos.xz) * p_weights.y;
	samp += texture(p_sampler, p_triplanar_pos.zy * vec2(-1.0, 1.0)) * p_weights.x;
	return samp;
}

void fragment() {
	vec4 albedo_tex;
	if (triplanar) {
		albedo_tex = triplanar_texture(texture_albedo, uv1_power_normal, uv1_triplanar_pos);
	} else {
		albedo_tex = texture(texture_albedo, UV);
	}

	if (overlay_texture) {
		vec4 albedo_tex_2;
		if (triplanar_2) {
			albedo_tex_2 = triplanar_texture(texture_albedo_2, uv2_power_normal, uv2_triplanar_pos);
		} else {
			albedo_tex_2 = texture(texture_albedo_2, UV);
		}
		ALBEDO = (albedo * albedo_tex * albedo_tex_2).rgb;
		ALPHA = albedo_tex.a * albedo_tex_2.a * albedo.a * ALPHA;
	} else {
		ALBEDO = (albedo * albedo_tex).rgb;
		ALPHA = albedo_tex.a * albedo.a * ALPHA;
	}

	if (proximity_fade) {
		// Proximity Fade: Enabled
		float proximity_depth_tex = textureLod(depth_texture, SCREEN_UV, 0.0).r;
		vec4 proximity_view_pos = INV_PROJECTION_MATRIX * vec4(SCREEN_UV * 2.0 - 1.0, proximity_depth_tex, 1.0);
		proximity_view_pos.xyz /= proximity_view_pos.w;
		ALPHA *= clamp(1.0 - smoothstep(proximity_view_pos.z + proximity_fade_distance, proximity_view_pos.z, VERTEX.z), 0.0, 1.0);
	}
}"
