[gd_resource type="Shader" load_steps=0 format=3 uid="uid://db2oqr6xgqa44"]

[resource]
code = "// NOTE: Shader automatically c;onverted from Godot Engine 4.4.dev6's StandardMaterial3D.

shader_type spatial;
render_mode blend_add,
	cull_disabled,
	depth_prepass_alpha,
	depth_draw_opaque,
	shadows_disabled,
	specular_disabled,
	unshaded;

uniform bool affine_mapping = false;
uniform float alpha_scissor : hint_range(0, 1) = 0.5;
uniform float jitter: hint_range(0.0, 1.0) = 0.25;
uniform ivec2 resolution = ivec2(480, 270);

uniform vec4 albedo : source_color = vec4(1.0);
uniform sampler2D texture_albedo : source_color, filter_nearest, repeat_enable;
uniform float point_size : hint_range(0.1, 128.0, 0.1) = 1.0;

uniform bool distance_fade = true;
uniform float distance_fade_min : hint_range(0.0, 4096.0, 0.01) = 0.0;
uniform float distance_fade_max : hint_range(0.0, 4096.0, 0.01) = 10.0;

varying vec3 uv1_triplanar_pos;
uniform float uv1_blend_sharpness : hint_range(0.0, 150.0, 0.001) = 1.0f;
varying vec3 uv1_power_normal;

uniform bool triplanar = false;

uniform vec3 uv1_scale = vec3(1.0);
uniform vec3 uv1_offset;
uniform vec3 uv2_scale;
uniform vec3 uv2_offset;

uniform bool equipped = false;
uniform float fov_override : hint_range(1.0, 179.0, 0.1) = 50.;

vec4 snap_to_position(vec4 base_position)
{
	vec4 snapped_position = base_position;
	snapped_position.xyz = base_position.xyz / base_position.w;

	vec2 snap_resulotion = floor(vec2(resolution) * (1.0 - jitter));
	snapped_position.x = floor(snap_resulotion.x * snapped_position.x) / snap_resulotion.x;
	snapped_position.y = floor(snap_resulotion.y * snapped_position.y) / snap_resulotion.y;

	snapped_position.xyz *= base_position.w;
	return snapped_position;
}

void vertex() {
	if (equipped) {
		Z_CLIP_SCALE = 0.01;

		float flip_y = sign(PROJECTION_MATRIX[1][1]);
		float aspect = PROJECTION_MATRIX[1][1] / PROJECTION_MATRIX[0][0];
		float f = flip_y / tan(fov_override * PI / 360.0);
		PROJECTION_MATRIX[0][0] = f / aspect;
		PROJECTION_MATRIX[1][1] = f;
	}
	vec3 normal = MODEL_NORMAL_MATRIX * NORMAL;

	TANGENT = vec3(0.0, 0.0, -1.0) * abs(normal.x);
	TANGENT += vec3(1.0, 0.0, 0.0) * abs(normal.y);
	TANGENT += vec3(1.0, 0.0, 0.0) * abs(normal.z);
	TANGENT = inverse(MODEL_NORMAL_MATRIX) * normalize(TANGENT);

	BINORMAL = vec3(0.0, 1.0, 0.0) * abs(normal.x);
	BINORMAL += vec3(0.0, 0.0, -1.0) * abs(normal.y);
	BINORMAL += vec3(0.0, 1.0, 0.0) * abs(normal.z);
	BINORMAL = inverse(MODEL_NORMAL_MATRIX) * normalize(BINORMAL);
//
	vec4 snapped_position = snap_to_position(PROJECTION_MATRIX * MODELVIEW_MATRIX * vec4(VERTEX, 1.0));
	if (affine_mapping)
	{
		POSITION = snapped_position;
		POSITION /= abs(POSITION.w);
	}
	else
	{
		POSITION = snapped_position;
	}

	if (triplanar) {

		// UV1 Triplanar: Enabled (with World Triplanar)
		uv1_power_normal = pow(abs(normal), vec3(uv1_blend_sharpness));
		uv1_triplanar_pos = (MODEL_MATRIX * vec4(VERTEX, 1.0)).xyz * uv1_scale + uv1_offset;
		uv1_power_normal /= dot(uv1_power_normal, vec3(1.0));
		uv1_triplanar_pos *= vec3(1.0, -1.0, 1.0);
	}
}

vec4 triplanar_texture(sampler2D p_sampler, vec3 p_weights, vec3 p_triplanar_pos) {
	vec4 samp = vec4(0.0);
	samp += texture(p_sampler, p_triplanar_pos.xy) * p_weights.z;
	samp += texture(p_sampler, p_triplanar_pos.xz) * p_weights.y;
	samp += texture(p_sampler, p_triplanar_pos.zy * vec2(-1.0, 1.0)) * p_weights.x;
	return samp;
}


void fragment() {
	vec4 albedo_tex;
	if (triplanar) {
		albedo_tex = triplanar_texture(texture_albedo, uv1_power_normal, uv1_triplanar_pos);
	} else {
		albedo_tex = texture(texture_albedo, UV);
	}

	ALBEDO = (albedo * albedo_tex).rgb;
	ALPHA = albedo_tex.a * albedo.a * COLOR.a;

	if (distance_fade) {
		ALPHA *= clamp(smoothstep(distance_fade_min, distance_fade_max, length(VERTEX)), 0.0, 1.0);
	}
	
	if (ALPHA == 0.0) {
		discard;
	}
}
"
